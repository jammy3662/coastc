
start
block: [statement] ...

statement:

	NAME ':'
|	'case' (expression | range_expression) <':'>
|	'default' <':'>
|	'continue' <';'>
|	'break' <';'>
|	'goto' NAME <';'>
|	'return' < expression > <';'>
|	'if' expression scope <('else' scope)>
|	'switch' expression scope
|	'while' expression scope
|	'do' 'while' expression scope
|	'do' scope 'while' expression <';'>
|	'for' iterator scope
|	'iterator' iterator_define <';'>
|	'operator' definable_operator tuple scope
|	line
...

line:

	note line
|	'alias' NAME '=' label <';'>
|	'macro' NAME  {.} '#'
|	'include' label <'as' NAME> <';'>
|	<generics> {qualifier} module <instances <';'>>
|	<generics> {qualifier} function
|	{qualifier} variables <';'>
|	expression <';'>
|	stack_frame
|	';'
...

note: 'note' {STR_CONST} ...

stack_frame: {qualifier} braced_scope ...

module:

	('struct' | 'union' | 'module') <label> scope
|	'enum' <label> enum_scope
...

scope:

	braced_scope
|	<':'> block '...'
...

enum_scope:

	'{'  <instances> '}'
|	<':'> <instances> '...'
...

braced_scope: '{' block '}' ...

generics: '[' <[label]> ']'...

iterator:

	'(' iterator ')'
|	loop_iterator
|	label ':' range_expression
|	label ':' expression
...

iterator_define:

	'(' iterator_define ')'
|	loop_iterator
|	range_expression
|	expression
...

loop_iterator: line expression ';' line ...

label: NAME {'.' NAME}...

variable: type instance ...

variables: type <instance {',' instance}> ...

instance: label {'[' expression ']'} <initializer | '..'> ...

initializer: '=' expression ...

function: type label tuple scope ...

tuple: '(' <parameters> ')' ...

parameters: variable {',' variable} ...

type:

	{type_qualifier} data {pointer}
|	type tuple
...

data:

	'let'
|	builtin
|	tuple
|	'typeof' label
|	label
...

expression:

	atomic
|	prefix expression
|	expression infix expression
|	expression postfix
|	expression expression
|	expression ',' expression
...

atomic:

	'(' <expression> ')'
|	'[' <expression> ']'
|	value
...

value:

	label
|	literal
|	meta label
...

literal: INT_CONST | FLOAT_CONST | CHAR_CONST | STR_CONST ...

range_expression: expression '..' expression ...

pointer:

	'^' <'const'> // owning pointer (reference)
|	'~' <'const'> // non-owning pointer (link)
|	'*' <'const'> // star pointer (user-managed memory)
...

builtin:

	'bit'
|	'char'
|	'byte'
|	'short' <'int'>
|	['long'] <'int'>
|	'int'
|	'float'
|	['long'] 'double'
...

qualifier: 'local' | 'static' | 'extern' | 'inline' ...

type_qualifier: 'const' | 'signed' | 'unsigned' | 'complex' | 'imaginary' ...

meta: 'sizeof' | 'countof' | 'nameof' | 'stringof' ...

prefix:

	'++'
|	'--'
|	'+'
|	'-'
|	'!'
|	'&'
|	'*'
|	'~'
...

postfix: '++' | '--' ...

infix:

	('and' | '&&')
|	( 'or' | '||')
	
|	'==' | '!='
	
|	'<' | '<=' | '>=' | '>'
	
|	arithmetic
|	arithmetic '='
|	'='
...

arithmetic:

	  '+' |  '-'
|	  '*' |  '/' | '%'
|	  '&' |  '^' | '|'
|	 '<<' | '>>'
|	'<<<' | '>>>'
...

definable_operator:

	'++' | '--' |   '+' |   '-' | '%'
|	 '&' |  '*' |   '~' |   '/'
|	'<<' | '>>' | '<<<' | '>>>'
...
